import java.text.DateFormat
import java.text.SimpleDateFormat

/*
 * Copyright 2020 Andrey Kitaev. All rights reserved.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId rootProject.appId
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding = true
    }

    configurations {
        ktlint
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        changeApkFileName(variant)
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

def changeApkFileName(variant) {
    variant.outputs.all { output ->
        outputFileName = 'event_picker_' + versionCode + '_' + versionName + '_' + getDateTime() + '.apk'
    }
}

static def getDateTime() {
    DateFormat df = new SimpleDateFormat("yyyyMMddHHmm")
    return df.format(new Date())
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // ktlint
    ktlint 'com.github.shyiko:ktlint:0.31.0'

    // logger
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // material components
    implementation 'com.google.android.material:material:1.2.0'

    // navigation graph
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.0"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.0"

    // firebase analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:17.5.0'

    // auth
    implementation 'com.google.firebase:firebase-auth-ktx:19.3.2'
    implementation 'com.google.android.gms:play-services-auth:18.1.0'

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    // koin
    implementation "org.koin:koin-android:1.0.2"
}

apply from: 'ktlint.gradle'
apply plugin: 'com.google.gms.google-services'
